
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
    // СтандартныеПодсистемы.ПодключаемыеКоманды
    ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
    // Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
    // СтандартныеПодсистемы.ПодключаемыеКоманды
    ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
    // Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
    ПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТовары

&НаКлиенте
Процедура ТоварыКоличествоПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыЦенаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПриИзменении(Элемент)
	РассчитатьСуммуДокумента();
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСкидкаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыУслуги

&НаКлиенте
Процедура УслугиКоличествоПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиЦенаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиСкидкаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиПриИзменении(Элемент)
	РассчитатьСуммуДокумента();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура РассчитатьСуммуСтроки(ТекущиеДанные)
	
	// ++ БелГН Задача 02 07.05.2025
	// КоэффициентСкидки = 1 - ТекущиеДанные.Скидка / 100;
	КоэффициентСкидки = ВычеслитьОбщийКоэффициентСкидки(ТекущиеДанные);
	// --
	ТекущиеДанные.Сумма = ТекущиеДанные.Цена * ТекущиеДанные.Количество * КоэффициентСкидки;
	
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьСуммуДокумента()
	
	Объект.СуммаДокумента = Объект.Товары.Итог("Сумма") + Объект.Услуги.Итог("Сумма");
	
КонецПроцедуры

#Область ПодключаемыеКоманды

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
    ПодключаемыеКомандыКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПродолжитьВыполнениеКомандыНаСервере(ПараметрыВыполнения, ДополнительныеПараметры) Экспорт
    ВыполнитьКомандуНаСервере(ПараметрыВыполнения);
КонецПроцедуры

&НаСервере
Процедура ВыполнитьКомандуНаСервере(ПараметрыВыполнения)
    ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, ПараметрыВыполнения, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
    ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти

#Область ДК_ДобавленныеПроцедурыИФункции

&НаКлиенте
Функция ВычеслитьОбщийКоэффициентСкидки(ТекущиеДанные)
	// ++БелГН Задача 02 07.05.2025
	ОбщийКоэффициент = ТекущиеДанные.Скидка + Объект.ДК_СогласованнаяСкидка;
	
	Если ОбщийКоэффициент > 100 Тогда
		ОбщийКоэффициент = 100;
		ШаблонСообщения = НСтр("ru = 'Скидка превысила 100 по номенклатуре: %1'");
		ТекстСообщения = СтрШаблон(ШаблонСообщения,ТекущиеДанные.Номенклатура);
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;
	
	КоэффициентСкидки = 1 - ОбщийКоэффициент / 100;
	Возврат КоэффициентСкидки;	
	// --
КонецФункции
	
&НаКлиенте
Процедура ДК_КомандаПересчитатьСумму(Команда)
	// ++ БелГН Задача 02 07.05.2025
	ДК_ПересчитатьСуммыСтрокВсехТаблиц();
	// --  
КонецПроцедуры

&НаКлиенте
Процедура ДК_ПересчитатьСуммыСтрокВсехТаблиц()
	// ++ БелГН Задача 02 07.05.2025
	Для каждого Товар Из Объект.Товары Цикл
		РассчитатьСуммуСтроки(Товар);
	КонецЦикла;

	Для каждого Услуга Из Объект.Услуги Цикл
		РассчитатьСуммуСтроки(Услуга);
	КонецЦикла;

	РассчитатьСуммуДокумента();
	// --
КонецПроцедуры

&НаКлиенте
Процедура ДК_СогласованнаяСкидкаПриИзменении(Элемент)
	// ++ БелГН Задача 02 07.05.2025
	Если Объект.Товары.Количество() = 0 И Объект.Услуги.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ДК_СогласоватьСкидкуАсинх();
	// --
КонецПроцедуры

&НаКлиенте
Асинх Процедура ДК_СогласоватьСкидкуАсинх()
	// ++ БелГН Задача 02 07.05.2025
	ТекстВопроса = НСтр("ru = 'Пересчитать суммы с учетом согласованной скидки?'");
	Ответ = Ждать ВопросАсинх(ТекстВопроса,РежимДиалогаВопрос.ДаНет);
	
	Если Ответ = КодВозвратаДиалога.Нет Тогда
  	Возврат;
	КонецЕсли;     
	
	ДК_ПересчитатьСуммыСтрокВсехТаблиц();
	// --
КонецПроцедуры

#КонецОбласти

#КонецОбласти
